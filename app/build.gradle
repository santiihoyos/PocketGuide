plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

Properties properties = new Properties()
try {

    properties.load(project.rootProject.file("local.properties").newDataInputStream())
} catch (ignored) {

    //if local properties is missing we try to load CI properties because normally
    //when this exception happens is because build is running on CI container
    properties.load(project.rootProject.file("ci.properties").newDataInputStream())
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.santiihoyos.cc"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../test_keystore.jks")
            storePassword "santiihoyos"
            keyAlias "santiihoyos"
            keyPassword "santiihoyos"
        }
    }

    buildTypes {
        release {
            debuggable true
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "codeChallenge"

    productFlavors {

        //Flavor for Paradigma digital code challenge
        rickAndMorty {
            dimension "codeChallenge"
            applicationIdSuffix = ".rickAndMorty"
        }

        //Flavor for OpenBank code challenge
        marvel {
            dimension "codeChallenge"
            applicationIdSuffix = ".marvel"
            buildConfigField "String", "BASE_URL", '"https://gateway.marvel.com:443"'
            buildConfigField "String", "API_KEY", properties.getProperty("marvel.apiKey", "\"\"")
            buildConfigField "String", "PRIVATE_KEY", properties.getProperty("marvel.privateKey", "\"\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {

    def dependencies = rootProject.ext.dependencies

    implementation project(path: dependencies.moduleBase)

    //Feature Characters module
    implementation project(path: dependencies.moduleCharacters)

    //Dagger
    kapt dependencies.daggerCompiler
}
