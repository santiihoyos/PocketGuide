plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.santiihoyos.cc"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../test_keystore.jks")
            storePassword "11paths"
            keyAlias "11paths"
            keyPassword "11paths"
        }
    }

    buildTypes {
        release {
            debuggable true
            signingConfig signingConfigs.release //Comentar para Jenkins
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "codeChallenge"

    productFlavors {

        //Flavor for Paradigma digital code challenge
        rickAndMorty {
            dimension "codeChallenge"
            applicationIdSuffix = ".rickAndMorty"
        }

        //Flavor for OpenBank code challenge
        marvel {
            dimension "codeChallenge"
            applicationIdSuffix = ".marvel"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    def dependencies = rootProject.ext.dependencies

    implementation project(path: dependencies.moduleBase)

    //For Paradigma Code challenge feature for RickAndMorty
    rickAndMortyImplementation project(path: dependencies.moduleCharacters)

    //For OpenBank Code challenge feature for RickAndMorty
    marvelImplementation project(path: dependencies.moduleHeroes)

    //Dagger
    kapt dependencies.daggerCompiler
}
