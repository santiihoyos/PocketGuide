plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.santiihoyos.cc"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../test_keystore.jks")
            storePassword "11paths"
            keyAlias "11paths"
            keyPassword "11paths"
        }
    }

    buildTypes {
        release {
            debuggable false
            signingConfig signingConfigs.release //Comentar para Jenkins
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "codeChallenge"

    productFlavors {

        //Flavor for Paradigma digital code challenge
        rickAndMorty {
            dimension "codeChallenge"
            applicationIdSuffix = ".rickAndMorty"
        }

        //Flavor for OpenBank code challenge
        marvel {
            dimension "codeChallenge"
            applicationIdSuffix = ".marvel"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    def dependencies = rootProject.ext.dependencies

    //features
    implementation project(path: ':base')

    //For Paradigma Code challenge feature for RickAndMorty
    rickAndMortyImplementation project(path: ':characters')

    //For OpenBank Code challenge feature for RickAndMorty
    marvelImplementation project(path: ':heroes')

    //Dagger (dependency comes from :base only needs kapt)
    kapt dependencies.daggerCompiler

    //unit test libraries
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.6.28'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.ext:truth:1.3.0'
    androidTestImplementation 'com.google.truth:truth:1.0'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.3.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.3.0'

    // The following Espresso dependency can be either "implementation"
    // or "androidTestImplementation", depending on whether you want the
    // dependency to appear on your APK's compile classpath or the test APK
    // classpath.
    androidTestImplementation 'androidx.test.espresso:espresso-idling-resource:3.3.0'
}
